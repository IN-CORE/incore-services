apply plugin: 'java'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

//add dependencies for plugins needed to run the build
buildscript {
    repositories {
        maven { url 'http://download.osgeo.org/webdav/geotools/' }
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.4.0'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

//apply base plugins to the root, so we can run all the server
//webapps in a single gretty/jetty server
apply plugin: 'idea'

idea {
    module {
        jdkName = '1.8'
    }
}

//define repositories needed for all builds
//(this is different than the buildscript block.
//  The buildscript repositories are for RUNNING the build script,
//  This is for repositories needed to find dependencies of the projects)
repositories {
    maven { url 'http://download.osgeo.org/webdav/geotools/' }
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://repo.boundlessgeo.com/main/" }
}


apply plugin: 'war'

if (!project.plugins.findPlugin(org.akhikhl.gretty.GrettyPlugin))
    project.apply(plugin: org.akhikhl.gretty.GrettyPlugin)

//a "farm" is the gretty term for multiple webapps that can be run
//in the same webapp service.
farm {
    webapp ':fragility-service'
    webapp ':data-service'
    webapp ':hazard-service'
    webapp ':maestro-service'
    webapp ':auth-service'
    webapp ':semantic-service'
}

task farmWars(dependsOn: war, type: Copy) {
    from('.') {
        include '**/*.war'
    }
    into('build')
}

//For now, all subprojects are java projects. Later this might change.
//Until then, give them all a basic java build
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.junit.platform.gradle.plugin'

    repositories {
        maven { url 'http://download.osgeo.org/webdav/geotools/' }
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "http://repo.boundlessgeo.com/main/" }
    }

    dependencies {
        compile "log4j:log4j:1.2.16"
        compile 'org.json:json:20160810'
        // Test Runtime
        testRuntime(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.2')
    }
}

test {
    testLogging {
        showStandardStreams = true
    }
}

//All of our service plugins have some common configuration.
// (war and gretty builds, some basic jersey and mongo dependencies, etc)
// Common configuration between them can be specified here like this.
["fragility-service", "data-service", "hazard-service", "maestro-service", "auth-service", "semantic-service"].each { name ->
    project(":$name") {

        apply plugin: 'war'
        if (!project.plugins.findPlugin(org.akhikhl.gretty.GrettyPlugin)) {
            project.apply(plugin: org.akhikhl.gretty.GrettyPlugin)
        }

        gretty {
            httpPort 8080
        }

        dependencies {
            compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
            compile 'org.glassfish.jersey.core:jersey-server:2.25.1'
            compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.25.1'
            compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.25.1'
            // Libraries for testing jersey controller actions
            testCompile(group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-grizzly2', version: '2.25.1')
            testCompile(group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-servlet', version: '2.25.1')
            // JUnit 5
            testCompile(group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.2')
            // Mocking Framework
            testCompile(group: 'org.mockito', name: 'mockito-core', version: '2.9.0')
            // Temporary requirement for optional junit dependency that should not be optional
            testCompile group: 'org.apiguardian', name: 'apiguardian-api', version: '1.0.0'
        }
    }
}
